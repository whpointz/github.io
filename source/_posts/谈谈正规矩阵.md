
---
title: 谈谈冯氏光照模型+正规矩阵
date: 2017-08-09 10:33:37
tags: [图形,数学]
mathjax: true
---
最近在写OpenGL着色器的时候遇到了一件之前没注意的事情：**正规矩阵**，在[光照基础](https://learnopengl-cn.readthedocs.io/zh/latest/02%20Lighting/02%20Basic%20Lighting/)这篇博客中的[这一部分](https://learnopengl-cn.readthedocs.io/zh/latest/02%20Lighting/02%20Basic%20Lighting/#_6)，当时没仔细，现在重新来看一遍。

先简单的介绍一下冯氏光照模型。

# 冯氏光照模型
冯氏光照模型是对现实中光照的一种简单的模拟，其主要结构由三个元素组成：
- 环境(Ambient)光照：无论如何永远都给物体一些颜色。
- 漫反射(Diffuse)光照：面向光源的一面比其他面会更亮。
- 镜面(Specular)光照：镜面光照的颜色，相比于物体的颜色更倾向于光的颜色。

其大致效果看起来是这样：
![mark](http://o9z9uibed.bkt.clouddn.com/image/20170809/104450818.png?imageslim)

# 环境光照
我们用光的颜色乘以一个(数值)很小常量环境因子，再乘以物体的颜色，然后使用它作为片段的颜色，这样来模拟环境光照。

片段着色器
``` c++
void main()
{
    float ambientStrength = 0.1f;
    vec3 ambient = ambientStrength * lightColor;
    vec3 result = ambient * objectColor;
    color = vec4(result, 1.0f);
}
```

# 漫反射光照
漫反射光使得物体上与光线排布越近的片段越能从光源处获得更多的亮度。

![mark](http://o9z9uibed.bkt.clouddn.com/image/20170809/105809962.png?imageslim)

图的左上方是一个光源，发出的光线落在物体的一个片段上。片段的亮度和光线与片段的角度有关。如果光线垂直于某个片段，那么这束光对此片段的影响最大，也就是说这个时候这个片段的表面是最亮的。

所以我们需要知道**光源的位置lightPos **，**片段的位置FragPos**以及**片段的法向量Normal**。

相对应的**顶点着色器**：
``` c++
layout (location = 0) in vec3 position;
layout (location = 1) in vec3 normal;

out vec3 FragPos;
out vec3 Normal;

uniform mat4 model;
uniform mat4 view;
uniform mat4 projection;

void main()
{
    gl_Position = projection * view * model * vec4(position, 1.0f);
    FragPos = vec3(model * vec4(position, 1.0f));//
    Normal = normal;
}
```

对应的**片段着色器**：
``` c++
out vec4 color;

in vec3 Normal;  
in vec3 FragPos;  

uniform vec3 lightPos;
uniform vec3 lightColor;
uniform vec3 objectColor;

void main()
{
    // Ambient
    float ambientStrength = 0.1f;
    vec3 ambient = ambientStrength * lightColor;

    // Diffuse
    vec3 norm = normalize(Normal);
    vec3 lightDir = normalize(lightPos - FragPos);
    float diff = max(dot(norm, lightDir), 0.0);//点积，两个向量垂直的时候，值为0
    vec3 diffuse = diff * lightColor;

    vec3 result = (ambient + diffuse) * objectColor;
    color = vec4(result, 1.0f);
}
```

# 正规矩阵
在顶点着色器中，我们把片段的位置`position`乘了`model`矩阵得到了片段的位置在空间中的位置`FragPos`。

那么对应起法向量normal呢，直接就可以用么？

法向量只是一个方向向量，不能表达空间中的特定位置。法向量没有齐次坐标(顶点位置中的w分量)。这意味着，平移不应该影响到法向量。也就是说，对于法向量，我们只能对它应用缩放(Scale)和旋转(rotation)变换。

当模型执行了不等比缩放的时候，法向量就不再垂直于表面了。
效果见下图：
![mark](http://o9z9uibed.bkt.clouddn.com/image/20170809/161553425.png?imageslim)

也就是说不能用这样的模型矩阵去乘以法向量。

那么怎么解决了，就是本次文章的核心内容：**正规矩阵(Normal Matrix)**。正规矩阵进行了线性代数操作移除了对法向量的错误缩放效果，[参考网址](http://www.lighthouse3d.com/tutorials/glsl-12-tutorial/the-normal-matrix/)。

见下图：
![mark](http://o9z9uibed.bkt.clouddn.com/image/20170809/172931151.png?imageslim)
根据上图：
- $T = P2 - P1$
- $N = Q2 - Q1$

已知:
- $T · N = 0$
- $T' = model * T$

求$N'$使得$N' · T' = 0$，假设一个变换矩阵$G$为我们所求的矩阵。

$N'$ $·$ $T'$ $ = (G \* N) · (model \* T)$

则将点乘换成乘法：
$(G \* N) · (model \* T)$ $= (G \* N)^T \* (Model \* T) = G^T \* N^T \* model \* T$

已知 $N^T \* T = 0$

所以当$G^T \* model = I$的时候，$G^T \* N^T \* model$才会是$N^T$，$N' · T'$才会是0。

故$G = (model^{-1})^T$

所以正规矩阵是这样的：
`Normal = mat3(transpose(inverse(model))) * normal;`


#  镜面光照
镜面光照(Specular Lighting)同样依据光的方向向量和物体的法向量，但是这次它也会依据观察方向，例如玩家是从什么方向看着这个片段的。
![mark](http://o9z9uibed.bkt.clouddn.com/image/20170809/175624386.png?imageslim)

通过反射法向量周围光的方向计算发射向量。然后计算反射向量和视线方向的角度，角度越小，镜面光的作用就越大。这样程序的效果就是当我们去看光被物体反射的那个方向的时候，会看到一个高光。

``` c++
// Specular
    vec3 viewDir = normalize(viewPos - FragPos);//视线的方向
    vec3 reflectDir = reflect(-lightDir, norm);//反射光
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), material.shininess);//视线和反射光的点乘 与 反射指数的阶乘
    vec3 specular = light.specular * (spec * material.specular);  

```
